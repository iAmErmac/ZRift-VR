ACTOR Scrag
{
//$Category Quake Monster
  Health 80
  GibHealth 40
  PainChance 128
  Speed 16
  Radius 31
  Height 56
  Damage 10
  Mass 99999999
  Scale 0.3
  Monster
  DamageFactor "Dodge", 0.0
  PainChance "Dodge", 0
  DamageFactor "Rocketdamage", 0.8
  DamageFactor "grenade", 0.8
  DamageFactor "lavanails", 1.3
  DamageFactor "lavashells", 1.3
  PainChance "Pushed", 256
  +FLOAT
  +NOGRAVITY
  +DONTGIB
  SeeSound "wiz/sight"
  PainSound "wiz/pain"
  DeathSound "wiz/death"
  ActiveSound "wiz/active"
  Obituary "%o was scragged by a Scrag"
  States
  {
  Spawn:
    WIZ1 ABCDEFGHIJKLMNO 3 A_Look
    Loop
  See:
    WIZ2 ABCDEFGHIJKLMN 3 A_Chase
    Loop
  Missile:
    WIZ3 ABCDEF 3 A_FaceTarget
    WIZ3 F 0 A_PLaySound("Wiz/attack")
    WIZ3 G 3 A_CustomMissile("WizardAcid",50)
    WIZ3 HJKLMLKJH 3 A_FaceTarget
    WIZ3 I 3 A_CustomMissile("WizardAcid",50)
    WIZ3 HGFEDCBA 3
    Goto see
  Pain:
    WIZ4 A 0 A_ChangeFlag("NOPAIN", 1)
    WIZ4 A 3 A_pain
    WIZ4 BCD 3
    WIZ4 A 0 A_ChangeFlag("NOPAIN", 0)
    Goto See
  Death:
    TNT1 A 0 A_ChangeFlag("NOPAIN", 1)
    WIZ5 A 3
    WIZ5 B 3 A_Scream
    WIZ5 C 3
    WIZ5 D 3 A_NoBlocking
    WIZ5 EFG 3
    WIZ5 H -1 A_SetFloorClip
    Stop
  Pain.Pushed:
  XDeath:
    QZUM A 0 A_SpawnDebris ("GIB12")
    QZUM A 0 A_SpawnDebris ("GIB2")
    QZUM A 0 A_SpawnDebris ("GIB3")
    QZUM A 0 A_SpawnItemEx ("WizardHead", 0, 0, 0, 0, 0, 10)
    TNT1 A 1 A_PlaySound("GIBBED")
    Stop
  Raise:
    WIZ5 H 5
    WIZ5 H 1 A_Die("Cannibal")
    Loop
  Death.Cannibal:
    WIZ5 H 0 A_Fall
    WIZ5 H 0 A_JumpIFInventory("Counter",5,"Eaten")
    WIZ5 H 0 A_GiveInventory("Counter",1)
    WIZ5 H 2
    WIZ5 H -1
    Stop
  Eaten:
    QZUM A 0 A_SpawnItemEx ("WizardHead2", 1)
    TNT1 A 1 A_PlaySound("GIBBED")
    Stop
  }
}

ACTOR WizardAcid
{
  Radius 3
  Height 2
  Speed 18
  Damage (9)
  Projectile
  DeathSound "wiz/Hit"
  +NOEXTREMEDEATH
  +SKYEXPLODE
  States
  {
  Spawn:
    BAL7 A 0
    BAL7 A 0 A_SpawnItemEx("FireParticle",0,0,1,0,1)
    BAL7 A 0 A_SpawnItemEx("FireParticle",0,0,1,0,-1)
    BAL7 A 1 Bright
    BAL7 A 0 A_SpawnItemEx("FireParticle",0,0,1,0,1)
    BAL7 A 0 A_SpawnItemEx("FireParticle",0,0,1,0,-1)
    BAL7 B 1 Bright
    BAL7 A 0 A_SpawnItemEx("FireParticle",0,0,1,0,1)
    BAL7 A 0 A_SpawnItemEx("FireParticle",0,0,1,0,-1)
    BAL7 C 1 Bright
    BAL7 A 0 A_SpawnItemEx("FireParticle",0,0,1,0,1)
    BAL7 A 0 A_SpawnItemEx("FireParticle",0,0,1,0,-1)
    BAL7 D 1 Bright
    Loop
  Death:
    TNT1 A 1
    Stop
  }
}

actor GIB11
{
  Radius 4
  Height 4
  Speed 0
  Projectile
  Gravity 0.5
  BounceType Doom
  BounceFactor 0.5
  -NOGRAVITY
  +NOTELEPORT
  +NOBLOCKMAP
  +MOVEWITHSECTOR
  +ThruActors
  +ClientSideOnly
  health 2
  States
  {
  Spawn:
    GIBS A 2 A_SpawnItemEx("GibBlood",0,0,0)
    loop
  Death:
    GIBS B 0 A_Jump(192, 2, 3, 4)
    GIBS B 300 A_Gravity
    stop
    GIBS B 250 A_Gravity
    stop
    GIBS B 200 A_Gravity
    stop
    GIBS B 150 A_Gravity
    stop
  }
}


actor GIB0
{
  Radius 4
  Height 4
  Speed 0
  +NOTELEPORT
  +NOBLOCKMAP
  +MOVEWITHSECTOR
  +ThruActors
  +ClientSideOnly
  States
  {
  Spawn:
    GIBS B 1
    loop
  }
}

actor WizardHead : GIB11
{
  -ClientSideOnly
  Scale 0.7
  States
  {
  Death:
    GIBS B -1 A_Gravity
    stop
  }
}

actor WizardHead2 : GIB0
{
  -ClientSideOnly
  Scale 0.7
  States
  {
  }
}


ACTOR FireParticle
{
  Mass 5
  +NoClip
  +NoTeleport
  +nogravity
  +ClientSideOnly
  Scale 0.5
  States
  {
  Spawn:
    FPAR ABCDE 5
    Stop
  Death:
    TNT1 A 0
    Stop
  }
}