Actor ChasmAnimalSpawner : RandomSpawner Replaces Demon
{
	dropitem "ChasmMincer", 255, 8
	dropitem "ChasmPiggy", 255, 8
	dropitem "ChasmRaptor", 255, 8
	dropitem "ChasmHound", 255, 8
	dropitem "ChasmScorpy", 255, 8
}
Actor ChasmMincer
{
//$Category ChasmMonsterAlien
    Health 185
    Radius 22
    Height 42
    Speed 12
    PainChance 56
	PainChance "Laser", 112
	Scale 1
	Mass 400
    Monster
    +FLOORCLIP
	+DONTHARMSPECIES
	Species "Alien"
    SeeSound "Mincer/See"
    PainSound "Mincer/Pain"
    DeathSound "Mincer/Death"
	ActiveSound "Mincer/Active"
    Obituary "%o was killed by a Mincer."
	BloodColor "Red"
	BloodType "RedBlood", "RedBlood"
	Tag "Alien Mincer"
	
    States
    {
    Spawn:
	    MCI1 A 0 NoDelay
		MCI1 A 0 A_Jump(10, "Idle1")
        MCI1 ABCDEFGHIJKLMNOPQR 2 A_Look
        Loop
	Idle1:
	    TNT1 A 0 A_PlaySound("Mincer/Idle",3)
		MCI1 R 1 A_Look
		Goto Spawn
    See:
        MCW1 ABCD 2 A_Chase
		TNT1 A 0 A_PlaySound("Alien/Walk", 4, 0.50)
		MCW1 EFFG 2 A_Chase
		TNT1 A 0 A_PlaySound("Alien/Walk", 4, 0.50)
        Loop
	Melee:
	    TNT1 A 0 A_Jump(100, "Melee1")
		TNT1 A 0 A_Jump(100, "Melee2")
		MCA1 ABCDEFGHI 2 A_FaceTarget
		MCA1 J 0 A_CustomBulletAttack(1, 1, 1, 6, "MincerMeleePuff", 50, CBAF_AIMFACING)
		MCA1 J 4
		MCA1 KLMNOP 2 A_FaceTarget
		Goto See
	Melee1:
		MCA2 ABCDEFGHI 2 A_FaceTarget
		MCA2 J 0 A_CustomBulletAttack(1, 1, 1, 6, "MincerMeleePuff", 50, CBAF_AIMFACING)
		MCA2 J 4
		MCA2 KLMNOP 2 A_FaceTarget
		Goto See
	Melee2:
		MCA3 ABCDEFGHIJ 2 A_FaceTarget
		MCA3 K 0 A_CustomBulletAttack(1, 1, 1, 6, "MincerMeleePuff", 50, CBAF_AIMFACING)
		MCA3 K 4
		MCA3 LMNOPQ 2 A_FaceTarget
		Goto See
	Pain:
		TNT1 A 0 A_Jump(40, "BigPain")
		MCP1 A 2 A_Pain
		MCP1 BCDEFGH 2
		Goto See
	BigPain:
		MCP2 A 2 A_Pain
		MCP2 BCDEFGHIJKLMNOPQRS 2 
		Goto See
	Death:
		TNT1 A 0 A_Jump(70, "Death2")
		MCD1 A 2
		MCD1 B 2 A_Scream
		MCD1 C 2 A_NoBlocking
		MCD1 DEFG 2
		MCD1 H 2 A_PlaySound("Alien/Fall", 4)
		MCD1 IJKLMNOPQ 2
		MCD1 R -1
		Stop
	Death2:
		MCD2 A 2
		MCD2 B 2 A_Scream
		MCD2 C 2 A_NoBlocking
		MCD2 DE 2
		MCD2 F 2 A_PlaySound("Alien/Fall", 4)
		MCD2 GHIJKLMNOPQR 2
		MCD2 S -1
		Stop
	Death.Explosive:
		TNT1 A 0
		Goto XDeath
	XDeath:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("Flesh/Gib", 4)
		TNT1 A 0 A_CustomMissile("GoreBloodSpawner", random(10,25), random(0,-20), random (0, 360), 2, random (0, 160))
		TNT1 A 0 A_CustomMissile("GoreBloodSpawner", random(10,25), random(0,20), random (0, 360), 2, random (0, 160))
		TNT1 AAA 0 A_CustomMissile("GoreSpawner", random(10,25), random(0,-20), random (0, 360), 2, random (0, 160))
		TNT1 AAA 0 A_CustomMissile("GoreSpawner", random(10,25), random(0,20), random (0, 360), 2, random (0, 160))
		TNT1 AAA 0 A_CustomMissile("BloodGiblet", random(10,20), random(0, -20), random (0, 360), 2, random (0, 160))
		TNT1 AAA 0 A_CustomMissile("BloodGiblet", random(10,20), random(0, 20), random (0, 360), 2, random (0, 160))
		Stop
	Raise:
		TNT1 A 0 A_CustomMissile("PhantomRezFX", 1, 5, random (0, 180), 2, random (0, 90))
		MCI1 A 1 A_SetTranslucent(0.10, 0)
		MCI1 ABCDEFGHJIKL 2 A_FadeIn(0.1)
		Goto See
    }
}

actor MincerMeleePuff
{
  +NOBLOCKMAP
  +NOGRAVITY
  +PUFFONACTORS
  Scale 0.3
  RenderStyle Add
  Alpha 0.95
  seesound "Mincer/Stab"
  states
  {
  Spawn:
    TNT1 A 0
    stop
  Crash:
    TNT1 A 0
    stop
  Melee:
	TNT1 A 0
    stop
  }
}