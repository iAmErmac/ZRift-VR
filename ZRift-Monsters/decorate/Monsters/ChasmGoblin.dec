Actor ChasmGoblin Replaces WolfensteinSS
{
//$Category ChasmMonsterFantasy
    Health 110
    Radius 18
    Height 54
    Speed 6
    PainChance 40
	PainChance "Laser", 82
	Scale 1
	Mass 500
    Monster
    +FLOORCLIP
	+DONTHARMSPECIES
	Dropitem "LaserBoltAmmo", 100
	Dropitem "ChasmCrossbow", 25
    SeeSound "Goblin/See"
    PainSound "Goblin/Pain"
    DeathSound "Goblin/Death"
	ActiveSound "Goblin/Active"
    Obituary "%o was killed by a Goblin."
	BloodColor "Red"
	BloodType "RedBlood", "RedBlood"
	Species "Fantasy"
	Tag "Gobbo"
    States
    {
    Spawn:
	    TNT1 A 0 NoDelay
        GBI1 ABCDEFGHIJKLMNOPQRST 2 A_Look
        Loop
    See:
        GBW1 ABCD 2 A_Chase
		TNT1 A 0 A_PlaySound("MongA/Walk", 4)
		GBW1 EFGH 2 A_Chase
		GBW1 IJKL 2 A_Chase
        Loop
	Missile:
	   	GBA1 ABC 4 A_FaceTarget
		TNT1 A 0 A_PlaySound("Goblin/Fire", 1)
		GBA1 D 0 A_CustomMissile("GoblinBolt",36,3)
		GBA1 DE 4 BRIGHT
		GBA1 HIJK 4
		TNT1 A 0 A_PlaySound("Goblin/Load", 1)
		GBA1 LMNOP 4
		Goto See
	Melee:
	    TNT1 A 0 A_Jump(50, "Melee1")
		TNT1 A 0 A_Jump(50, "Melee2")
	    GBA2 ABCDEFGH 2 A_FaceTarget
		GBA2 I 0 A_CustomBulletAttack(1, 1, 1, 8, "GruntMeleePuff", 50, CBAF_AIMFACING)
		GBA2 I 2
		GBA2 JKLMNOPQR 2
		Goto See
	Melee1:
	    GBA3 ABCDEFGHIJK 2 A_FaceTarget
		GBA3 L 0 A_CustomBulletAttack(1, 1, 1, 8, "GruntMeleePuff", 50, CBAF_AIMFACING)
		GBA3 L 2
		GBA3 MNOPQRST 2
		Goto See
	Melee2:
	    GBA4 ABCDEFGH 2 A_FaceTarget
		GBA4 I 0 A_CustomBulletAttack(1, 1, 1, 8, "GruntMeleePuff", 50, CBAF_AIMFACING)
		GBA4 I 2
		GBA4 JKLMNOPQ 2
		Goto See
  Pain:
    GBP1 A 2 A_Pain
    GBP1 BCDEFGHI 2
    Goto See
  Death:
    TNT1 A 0 A_Jump(180, "Death2")
    TNT1 A 0 A_Pain
	GBD1 ABCDEFG 2
    GBD1 H 6 
    GBD1 I 6 A_NoBlocking
    GBD1 JKLMN 5
	TNT1 A 0 A_Scream
	GBD1 OPQR 4
	TNT1 A 0 A_PlaySound("MongA/Fall", 4)
    GBD1 ST 4
	GBD1 T -1
    Stop
  Death2:
    TNT1 A 0 A_Scream
	GBD2 ABCDEFG 2
	TNT1 A 0 A_PlaySound("MongA/Fall", 4)
    GBD2 H 2
    GBD2 I 2 A_NoBlocking
    GBD2 JKLMNOPQR 2
    GBD2 STUVWXYZ 2
	GBD2 Z -1
    Stop
  Death.Explosive:
    TNT1 A 0
	Goto XDeath
  XDeath:
    TNT1 A 0
	TNT1 A 0 A_NoBlocking
    TNT1 A 0 A_PlaySound("Flesh/Gib", 7)
    TNT1 A 0 A_CustomMissile("GoreBloodSpawner", random(45,60), random(0,-20), random (0, 360), 2, random (0, 160))
	TNT1 A 0 A_CustomMissile("GoreBloodSpawner", random(45,60), random(0,20), random (0, 360), 2, random (0, 160))
	TNT1 AAAA 0 A_CustomMissile("GoreSpawner", random(40,55), random(0,-20), random (0, 360), 2, random (0, 160))
	TNT1 AAAA 0 A_CustomMissile("GoreSpawner", random(40,55), random(0,20), random (0, 360), 2, random (0, 160))
	TNT1 AAA 0 A_CustomMissile("BloodGiblet", random(40,55), random(0, -20), random (0, 360), 2, random (0, 160))
	TNT1 AAA 0 A_CustomMissile("BloodGiblet", random(40,55), random(0, 20), random (0, 360), 2, random (0, 160))
    Stop
  Raise:
    TNT1 A 0 A_CustomMissile("PhantomRezFX", 1, 5, random (0, 180), 2, random (0, 90))
	GBI1 A 1 A_SetTranslucent(0.10, 0)
	GBI1 ABCDEFGHJIKL 2 A_FadeIn(0.1)
  Goto See
    }
}

ACTOR GoblinBolt
{
  Radius 12
  Height 8
  Speed 14
  Damage 4
  Projectile
  +RANDOMIZE
  RenderStyle Add
  Alpha 0.97
  DeathSound "Viking/Exp"
  States
  {
  Spawn:
  	TNT1 A 0 NoDelay
    CRS1 X 1 Bright
    Loop
  Death:
	TNT1 A 0 A_Scream
	TNT1 AAAA 0 A_SpawnParticle ("Orange", SPF_FULLBRIGHT | SPF_RELATIVE, random(25,45), random(2,3), frandom(0,360), 0,0,0, frandom(0.2,12.0),frandom(-0.2,0.2),frandom(-4.0,4.0), 0,0,-0.1, 0.98, -1)
	TNT1 A 0 A_CustomMissile("LaserExpFX", random(1,6), random(5,-5), random (0, 360), 2, random (0, 160))
	TNT1 AAAA 0 A_CustomMissile("EnemyExplosionFlare", random(1,8), random(10,-10), random (0, 360), 2, random (0, 160))
	TNT1 A 0 A_CustomMissile("EnemyExplosionSparkSpawner", random(1,8), random(15,-15), random (0, 360), 2, random (0, 160))
    TNT1 A 0 A_CustomMissile("EnemyExplosionSmokeSpawner", random(1,8), random(15,-15), random (0, 360), 2, random (0, 160))
	Stop
  }
}

